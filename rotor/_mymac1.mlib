nachalo
!>
/prep7  
*afun,deg
/PLOPTS, minm, 0
/PLOPTS, FRAME, 0
/PLOPTS, leg1, 0
/triad,lbot 

*dim,opora,,10000
*dim,opora2,,10000
i=0
j=0

*dim,mk,,100
*dim,dk,,100
*dim,lk,,100

*dim,diam,,100
*dim,hiam,,100
*dim,diamg,,100
*dim,hiamg,,100
!<
/eof

svoistva
!> свойства материала

  emod1=2.05e+11  			!модуль упругости
  dens1=7850				!плотность
  mp,ex,1,emod1
  mp,dens,1,dens1
  mp,alpx,1,13.1e-6
  mp,mu,101,0.2
  
  emod2=2.05e+11  			!модуль упругости
  dens2=7850				!плотность
  mp,ex,2,emod1
  mp,dens,2,dens1
  
  emod3=2.05e+11  			!модуль упругости
  dens3=7850				!плотность
  mp,ex,3,emod1
  mp,dens,3,dens1
  
!<
/eof

oporatr
!>
do=0.5
ho=do/2

 i=i+1
 j=j+1
  
*get,opora(i),node,0,num,maxd
*get,xo1,node,opora(i),loc,x
*get,yo1,node,opora(i),loc,y
*get,zo1,node,opora(i),loc,z

gap=0.001

*if,yo1,le,d/2,then
yo1o=visota_opori_pod_zemlei
*else
yo1o=yo1-gap-d/2
*endif

n,opora2(j),xo1,yo1-gap-d/2,zo1
*get,opora2(j),node,0,num,maxd
knode,,opora2(j)
et,5,contac52
type,5
r,5,1e10,gap
real,5
e,opora(i),opora2(j)

*if,yo1o,ne,0,then
pspec,1,,,do,ho
branch,opora2(j)
run,0,-yo1o,0,1
*get,zemlya,elem,0,num,maxd
/color,elem,dgra,zemlya
*else
*endif


k,i,xo1,-yo1o-d/2,zo1

branch,opora(i)

pspec,1,,,d,h

!<
/eof

bigif
!> 
*if,tip,eq,0,then													!esli tip = 0		(nachalo)
!>
x0=0
y0=0
z0=0

!<

*elseif,tip,eq,1,then												!esli tip = 1		(bila pipe ili opora do etogo)
!>
run,x1,y1,z1,((x1**2+y1**2+z1**2)**(1/2))/edlin

x0=0
y0=0
z0=0
!<

*elseif,tip,eq,2,then												!esli tip = 2		(bil otvod)
!>
pspec,1,,,dg,hg

x0=(1.51*x*d)/(x**2+y**2+z**2)**(1/2)
y0=(1.51*y*d)/(x**2+y**2+z**2)**(1/2)
z0=(1.51*z*d)/(x**2+y**2+z**2)**(1/2)

run,x0,y0,z0

bend,,,,benddiv,
!<

*elseif,tip,eq,3,then												!esli tip = 3		(bil kran)
!>
pspec,1,,,dk(k),hk

x0=x1
y0=y1
z0=z1

run,x0,y0,z0,((x0**2+y0**2+z0**2)**(1/2))/edlin
!<

*elseif,tip,eq,4,then												!esli tip = 4		(bila opora)
!>
pspec,1,,,d,h
run,x1,y1,z1,((x1**2+y1**2+z1**2)**(1/2))/edlin
*use,oporatr

x0=0
y0=0
z0=0
!<
*endif
!<
/eof

end
!>
*use,bigif

x=x-x0
y=y-y0
z=z-z0

pspec,1,,,d,h

run,x,y,z,((x**2+y**2+z**2)**(1/2))/edlin

x=0
y=0
z=0

tip=0
!<
/eof

pipe
!>
*use,bigif



pspec,1,,,d,h

x1=x-x0
y1=y-y0
z1=z-z0

x=0
y=0
z=0

tip=1

!<
/eof

otvod
!> 
*Use,bigif


pspec,1,,,d,h

x=x-x0
y=y-y0
z=z-z0

x1=(1.51*x*d)/(x**2+y**2+z**2)**(1/2)
y1=(1.51*y*d)/(x**2+y**2+z**2)**(1/2)
z1=(1.51*z*d)/(x**2+y**2+z**2)**(1/2)

x=x-x1
y=y-y1
z=z-z1

run,x,y,z,((x**2+y**2+z**2)**(1/2))/edlin

pspec,1,,,dg,hg

run,x1,y1,z1

x=0
y=0
z=0

tip=2

!<
/eof

kran
!>
*use,bigif

k=arg1

x=x-x0
y=y-y0
z=z-z0

x1=(lk(k)/2)*(x/(x**2+y**2+z**2)**(1/2))
y1=(lk(k)/2)*(y/(x**2+y**2+z**2)**(1/2))
z1=(lk(k)/2)*(z/(x**2+y**2+z**2)**(1/2))

x=x-x1
y=y-y1
z=z-z1

pspec,1,,,d,h

run,x,y,z,((x**2+y**2+z**2)**(1/2))/edlin

hk=dk(k)-sqrt((dk(k))**2-(4*mk(k))/(3.14*lk(k)*dens1))

pspec,1,,,dk(k),hk
run,x1,y1,z1,((x1**2+y1**2+z1**2)**(1/2))/edlin

x=0
y=0
z=0

tip=3
!<
/eof

kran1
!>
*use,bigif

k=arg1

x=x-x0
y=y-y0
z=z-z0

x1=(lk(k)/2)*(x/(x**2+y**2+z**2)**(1/2))
y1=(lk(k)/2)*(y/(x**2+y**2+z**2)**(1/2))
z1=(lk(k)/2)*(z/(x**2+y**2+z**2)**(1/2))

x=x-x1
y=y-y1
z=z-z1

pspec,1,,,d,h

run,x,y,z,((x**2+y**2+z**2)**(1/2))/edlin

hk=dk(k)-sqrt((dk(k))**2-(4*mk(k))/(3.14*lk(k)*dens1))

pspec,1,,,dk(k),hk
run,x1,y1,z1,((x1**2+y1**2+z1**2)**(1/2))/edlin

*use,oporatr

x=0
y=0
z=0

tip=3
!<
/eof

opora
!>
*use,bigif

pspec,1,,,d,h

x1=x-x0
y1=y-y0
z1=z-z0

x=0
y=0
z=0

tip=4

!<
/eof

pipespec
!>
p=arg1

d=diam(p)
h=hiam(p)
dg=diamg(p)
hg=hiamg(p)

edlin=0.3
benddiv=6

!<
/eof

podzem
!>
xp=x
yp=y
zp=z


promopm=(((x**2+y**2+z**2)**(1/2))/promop)-1
*do,krr,1,promopm,1
x=(xp/(xp**2+yp**2+zp**2)**(1/2))*promop
y=(yp/(xp**2+yp**2+zp**2)**(1/2))*promop
z=(zp/(xp**2+yp**2+zp**2)**(1/2))*promop
*use,opora
*enddo

ost=(xp**2+yp**2+zp**2)**(1/2)-krr*promop

x=(xp/(xp**2+yp**2+zp**2)**(1/2))*ost
y=(yp/(xp**2+yp**2+zp**2)**(1/2))*ost
z=(zp/(xp**2+yp**2+zp**2)**(1/2))*ost
!<
/eof
