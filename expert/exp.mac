finish
/clear

/prep7  
*afun,deg

!>	Параметры

!>	Материал

emod1	=	2.05e+11  			!	модуль упругости
dens1	=	7850				!	плотность
alpx1	=	13.1e-6				!	коэффициент температурного расширения
mu1		=	0.2					!	модуль сдвига
EX1		=	2.06e11				!	Модуль Юнга
NUXY	=	0.3					!	Коэффициент Пуассона

!>	Данные для реальной кривой деформирования

!<

!<

!>	Геометрия

D_tr	=	1.02			!	внешний диаметр трубы	(м)
R_tr	=	D_tr/2			!	внешний радиус трубы	(м)
Th_tr	=	0.014			!	толщина стенки трубы	(м)
L_tr	=	17				!	длина участка	(м)

i_r		=	1				!	количество нодов по толщине (=	кол-во элементов + 1)
i_fi	=	36				!	количество нодов по окружности	( = кол-во элементов)
i_L		=	170			!	Количество нодов по длине	(=	кол-во элементов + 1)

!<

!>	Нагрузки 

P_vn	=	0! 7500000				!	внутреннее давление (Па)
Hz = 2
Ro = 2000

!<

!<
!>	 Свойства материала 

	mp,	ex,	1,	emod1
	mp,	dens,	1,	dens1
	mp,	alpx,	1,	alpx1
	mp,	mu,	101,	mu1
	mp,	EX,	1,	EX1
	mp,	NUXY,	1,	0.3

!<

!> Геометрия

local,	11,	1,	0,	0,	0,	0,	0,	0,	
csys, 	11

!	уровень по длине i_L

*do,	i,	0,	i_L

!	уровень по окружности, i_fi

*do,	j, 1, i_fi


	n, , R_tr, j*360/i_fi, -i*L_tr/i_L

*enddo

*enddo
!<

!> Элементы

et, 1, shell63
r,1,th_tr, th_tr, th_tr, th_tr
real, 1
type, 1

! уровень по длине i_L

*do,	i,	0,	i_L-1

! уровень по окружности, i_fi

*do,	j, 1, i_fi

	*if, j, eq, i_fi, then
	
	e, i*i_fi+j, i*i_fi+1, (i+1)*(i_fi)+1, (i+2)*(i_fi)
	
	*else
	
	e,  i*i_fi+j, i*i_fi+j+1, (i+1)*(i_fi)+j+1, (i+1)*(i_fi)+j
	
	*endif
	
*enddo

*enddo
!<

!> Нагрузки

!>! закрепление

nsel,	s,	loc,	y,	268,	272
nsel,	r,	loc,	z,	-8.155, -8.845
nsel,   u,  loc,    z,  -8.3, -8.7
d,	all,	all,	
nsel, all

!<

!> grunt

Vgr = L_tr*((Hz+D_tr/2)*D_tr-0.5*3.1415*(D_tr/2)**2)
Fgr = Vgr*Ro*9.8
Fvodi = 1000*L_tr*9.8*3.1415*(D_tr/2)**2

nsel,	s,	loc,	y,	0,	180
*GET, node_verh, NODE, 0, count

f, all, fy, -Fgr/node_verh
nsel, all

nsel, s, loc, y, 180, 360
*GET, node_niz, NODE, 0, count
f, all, fy, -Fvodi/node_niz

nsel, all

acel,0,9.81,0

!<
!<

/solu

solve


