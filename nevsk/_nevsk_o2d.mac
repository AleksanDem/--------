finish
/clear

/prep7  
*afun,deg

!>	Параметры

!>	Материал

emod1	=	2.05e+11  			!	модуль упругости
dens1	=	7850				!	плотность
alpx1	=	13.1e-6				!	коэффициент температурного расширения
!mu1		=	0.2					!	модуль сдвига
EX1		=	2.06e11				!	Модуль Юнга
NUXY	=	0.3					!	Коэффициент Пуассона

!<

!>	Геометрия

D_tr	=	0.72			!	внешний диаметр трубы	(м)
R_tr	=	D_tr/2			!	внешний радиус трубы	(м)
Th_tr	=	0.02			!	толщина стенки обечайки	(м)
Th_Dn	=	0.026			!	толщина стенки днища
L_tr	=	3*D_tr			!	длина участка	(м)
ova_tr	=	1				!	овальность трубы (%)
n		=	(2-ova_tr/100)/(2+ova_tr/100)		!	параметр элиптической системы координат (Dmax - горизонтальный диаметр)

i_r		=	5				!	количество нодов по толщине (=	кол-во элементов + 1)
i_fi	=	256				!	количество нодов по окружности	( = кол-во элементов)
i_L		=	14*i_r			!	Количество нодов по длине	(=	кол-во элементов + 1)

!<

!>	Нагрузки 

P_in = 7500000!+300000				!	внутреннее давление (Па)

!<

!<
!>	 Свойства материала 

	mp,	ex,	1,	emod1
	mp,	dens,	1,	dens1
	mp,	alpx,	1,	alpx1
!	mp,	mu,	1,	mu1
	mp,	EX,	1,	EX1
	mp,	NUXY,	1,	0.3

!<



!> Геометрия

Ld = 0.047										! длина фаски на переходе днище - обечайки
HO = 0.025										! толщина обечайки
HP = 0.016										! толщина патрубка
HD = 0.035										! толщина днища
R1 = 0.355-HD/2									! большой внешний радиус
R2 = 0.22-HD/2									! малые внешний радиус
h = 0.07										! длина прямой вставки в днище
L = 5.08 +2*Ld									! длина обечайки
dl = 0.04										! длина патрубка "внутрь"
rp = 0.162-HP/2									! радиус патрубка	
Lp = 0.212										! длина патрубка целиком
ETol = 4										! количество элементов по толщине
rad_sh = 0.01									! радиус скругления в сварном шве

k, 1,  0,-R2, 0									! начальная точка для построения оси
k, 2, 0, 2*h+L+R2								! конечная точка для построения оси

k, 3, R1,0										! верхняя точка левого элипса
k, 4, -R1, 0									! нижняя точка левого элипса

local,11,1,0,0,0,,,,R2/r1						! задание локальной системы координат для левого внешнего эллипса (1)
csys, 11										! переход в СК(1)
l, 4, 3											! построение внешнего левого полуэллипса (правая половина)
csys, 0											! переход в начальную СК
lsymm,y,1,,,,,									! построение внешнего левого полуэллипса (левая половина)
k, 7, R1, h										! конечная точка прямого участка днища (внешняя)
k, 8, R1, h+L									! конечная точка обечайки (внешняя)
k, 9, R1, 2*h+L									! верхняя точка правого эллипса

l, 3, 7											! прямой участок левого днища
l, 7, 8											! обечайка (внешняя)
l, 8, 9											! прямой участок правого днища

k, 10, -R1, 2*h+L								! нижняя точка левого днища
local,12,1,0,2*h+L,0,,,,R2/R1					! задание локальной системы координат для правого внешнего эллипса (2)
csys, 12										! переход в СК(2)
l, 9, 10										! остроение внешнего правого полуэллипса (правая половина)
csys, 0											! переход в начальную СК

k, 11, rp, 2*h+L-dl+R2-hd*1.2					! левая точка патрубка
k, 12, rp, 2*h+L-dl+Lp+R2						! правая точка патрубка

l, 11, 12										! патрубок

l, 1, 2											! ось

lglue, 2,8										!
lglue, 6,8
lovlap, 2, 7

ldele, 1, 9, 8
ldele, 6, 11, 5

NUMMRG, all

arotat, 10, 3, 4, 5, 12, , 1, 2, 360
arotat, 13, 14, , , , , 1, 2, 360

NUMMRG, all

esize, 0.005

et, 1, shell43
r, 1, HD, HD, HD, HD
r, 2, HO, HO, HO, HO
r, 3, HP, HP, HP, HP

type, 1
real, 1
amesh, 1, 16, 5

type, 1
real, 1
amesh, 2, 17, 5


type, 1
real, 2
amesh, 3, 18, 5

type, 1
real, 2
amesh, 4, 19, 5

type, 1
real, 2
amesh, 5, 20, 5

type, 1
real, 3
amesh, 21, 28, 1

dl, 52, , all
dl, 55, , all
dl, 58, , all
dl, 61, , all


asel, u, , , 21, 27, 2

sfa, all, , pres, P_in

asel, all

nummrg, all

/solu
ANTYPE,STATIC   
nlgeom, on
OUTPR,BASIC,ALL

solve

/eof
